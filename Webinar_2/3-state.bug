
model {

# Multinomia Likelihood
# for (i in 1:3){ r[i, 1:3] ~ dmulti(P[i, 1:3], n[i])
#     }


q1 <- Q[1,2] + Q[1,3]
q2 <- Q[2,1] + Q[2,3]

h <- sqrt( (q1 - q2)^2 + 4 * Q[1,2] * Q[2,1] )
e1 <- exp(-.5*(q1 + q2 - h) * t) 
e2 <- exp(-.5*(q1 + q2 + h) * t) 


P[1,1] <- ( (-q1 + q2 + h) * e1 + (q1 - q2 + h) * e2) / (2 * h)
P[1,2] <- (( -q1 + q2 + h) * (q1 - q2 + h) * (e1 - e2)) / ( 4 * h * Q[2,1])
P[1,3] <- 1 - P[1,1] - P[1,2]
P[2,1]  <- Q[2,1] * (e1 - e2) / h
P[2,2] <- ((q1 - q2 + h) * e1 + (-q1 + q2 + h) * e2) / (2 * h)
P[2,3] <- 1 - P[2,1] - P[2,2]
P[3,1] <- 0
P[3,2] <- 0
P[3,3] <- 1


# Normalized transition probabilities are used to stabilize the model
# If p[1,j] is exactly 0 or NA due to symbolic math breakdowns 
# (e.g., division by row2_sum = 0), the likelihood becomes invalid
# triggering that dreaded “Invalid parent values” error.
# max(max(...), ε) acts like a soft-clipping threshold to guarantee numerical safety.

# ---------- p[1,j] ---------- 

row1_sum <- P[1,1] + P[1,2] + P[1,3]

p[1,1] <- max(max(P[1,1] / row1_sum, 0), 0.000001)
p[1,2] <- max(max(P[1,2] / row1_sum, 0), 0.000001)
p[1,3] <- max(max(P[1,3] / row1_sum, 0), 0.000001)


r[1,1:3] ~ dmulti(p[1,1:3], n[1])


# ---------- p[2,j] ---------- 

row2_sum <- P[2,1] + P[2,2] + P[2,3] 

p[2,1] <- max(max(P[2,1] / row2_sum, 0), 0.000001)
p[2,2] <- max(max(P[2,2] / row2_sum, 0), 0.000001)
p[2,3] <- max(max(P[2,3] / row2_sum, 0), 0.000001)


r[2,1:3] ~ dmulti(p[2,1:3], n[2])

# ---------- p[3,j] ---------- 
 
row3_sum <- P[3,1] + P[3,2] + P[3,3]


p[3,1] <- max(max(P[3,1] / row3_sum, 0), 0.000001)
p[3,2] <- max(max(P[3,2] / row3_sum, 0), 0.000001)
p[3,3] <- max(max(P[3,3] / row3_sum, 0), 0.000001)


r[3,1:3] ~ dmulti(p[3,1:3], n[3])






# Priors for transition rates

Q[1,2] ~ dgamma(0.1, 0.1)
Q[1,3] ~ dgamma(0.1, 0.1)

Q[1,1] <- -(Q[1,2] + Q[1,3])

# Priors from state 2
Q[2,1] ~ dgamma(0.1, 0.1)
Q[2,3] ~ dgamma(0.1, 0.1)

Q[2,2] <- -(Q[2,1] + Q[2,3])

# Priors for state 3 
Q[3,1] ~ dgamma(0.1, 0.1)
Q[3,2] ~ dgamma(0.1, 0.1)

Q[3,3] <- -Q[3,1] - Q[3,2]
}

